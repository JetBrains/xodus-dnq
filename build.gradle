buildscript {
    ext.kotlin_version = '1.2.30'
    ext.exodus_version = '1.2.3217'
    ext.dokka_version = '0.9.15'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'org.jetbrains.kotlin.jvm' version '1.2.0'
    id 'io.codearte.nexus-staging' version '0.11.0'
    id 'idea'
}

idea {
    project {
        jdkName = '1.8'
        vcs = 'Git'
    }
    module {
        downloadSources = true
    }
}

allprojects {
    sourceCompatibility = 1.8
    group = 'org.jetbrains.xodus'
    version = (project.hasProperty("teamcity")) ? project.teamcity["build.number"] : 'dev'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'license'
    apply plugin: 'org.jetbrains.dokka'

    repositories {
        mavenCentral()
        maven { url 'http://repository.jetbrains.com/xodus' }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile 'io.github.microutils:kotlin-logging:1.4.7'

        testCompile 'junit:junit:4.12'
        testCompile 'org.slf4j:slf4j-simple:1.7.21'
    }

    license {
        header rootProject.file('license/copyright.ftl')
        strictCheck true
        ext.inceptionYear = 2006
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.owner = 'JetBrains s.r.o.'
        include "**/*.kt"
        include "**/*.java"
        mapping {
            kt = 'JAVADOC_STYLE'
        }
    }

    test {
        minHeapSize = '512m'
        maxHeapSize = '512m'
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
        outputs.upToDateWhen { false }
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        duplicatesStrategy 'exclude'
        includeEmptyDirs false
        from project.sourceSets.main.java
        from project.sourceSets.main.kotlin
    }

    // this is a stub javadoc until we investigate and fix the following docklet error somehow:
    // java.lang.ClassCastException: com.sun.tools.javac.code.Type$AnnotatedType cannot be cast to com.sun.tools.javac.code.Type$ClassType
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from "${rootProject.projectDir}/README.MD"
    }

    artifacts {
        archives jar, sourceJar, javadocJar
    }
}

apply from: 'publishing.gradle'
