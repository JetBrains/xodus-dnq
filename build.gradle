buildscript {
    ext.kotlin_version = '1.8.10'
    ext.exodus_version = '9.9.60'
    ext.dokka_version = '1.7.20'
    ext.log4j_version = '2.17.1'
    ext.google_truth_version = '1.1.3'
    ext.junit_version = '4.13.2'

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'io.codearte.nexus-staging' version '0.21.1'
    id 'de.marcphilipp.nexus-publish' version '0.4.0' apply false
    id 'idea'
}


idea {
    project {
        jdkName = '21'
        vcs = 'Git'
    }
    module {
        downloadSources = true
    }
}

allprojects {
    group = 'org.jetbrains.xodus'
    version = '3.0.' + ((project.hasProperty("teamcity")) ? project.teamcity["build.number"] : 'dev')

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://packages.jetbrains.team/maven/p/xodus/xodus-daily' }
        maven { url = uri("https://packages.jetbrains.team/maven/p/xodus/orientdb-daily") }
        // necessary for dokka 1.4.30
        maven { url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven" }
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'license'
    apply plugin: 'org.jetbrains.dokka'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation 'io.github.microutils:kotlin-logging:3.0.5'

        testImplementation "junit:junit:$junit_version"
        testImplementation "org.apache.logging.log4j:log4j-1.2-api:$log4j_version"
        testImplementation "org.apache.logging.log4j:log4j-api:$log4j_version"
        testImplementation "org.apache.logging.log4j:log4j-core:$log4j_version"
        testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    }

    license {
        header rootProject.file('license/copyright.ftl')
        strictCheck true
        ext.inceptionYear = 2006
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.owner = 'JetBrains s.r.o.'
        include "**/*.kt"
        include "**/*.java"
        mapping {
            kt = 'JAVADOC_STYLE'
        }
    }

    tasks.withType(compileKotlin.class).all {
        kotlinOptions {
            jvmTarget = "17"
            apiVersion = "1.8"
            languageVersion = "1.8"
        }
    }


    test {
        minHeapSize = '512m'
        maxHeapSize = '512m'
        systemProperty 'exodus.tests.buildDirectory', project.buildDir
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
        outputs.upToDateWhen { false }
    }

    dokkaJavadoc {
        dokkaSourceSets {
            configureEach {
                reportUndocumented.set(false)
            }
        }
    }

    tasks.register('sourceJar', Jar) {
        archiveClassifier = 'sources'
        duplicatesStrategy 'exclude'
        includeEmptyDirs false
        from project.sourceSets.main.java
        from project.sourceSets.main.kotlin
    }

    // this is a stub javadoc until we investigate and fix the following docklet error somehow:
    // java.lang.ClassCastException: com.sun.tools.javac.code.Type$AnnotatedType cannot be cast to com.sun.tools.javac.code.Type$ClassType
    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from "${rootProject.projectDir}/README.MD"
    }
}

apply from: 'publishing.gradle'
